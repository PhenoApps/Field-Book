apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    signingConfigs {
        playStoreConfig {
            Properties keystoreProps = new Properties()
            if (file('keystore.config').exists()) {
                keystoreProps.load(new FileInputStream(file('keystore.config')))

                keyAlias keystoreProps['keyAlias']
                keyPassword keystoreProps['keyPassword']
                storePassword keystoreProps['storePassword']
                storeFile file('keystore.keystore')
            } else println("Non-keystore build.")
        }
    }

    compileSdkVersion 29

    defaultConfig {
        applicationId "com.fieldbook.tracker"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode = 510
        versionName = '5.1.0'
        multiDexEnabled true
    }

    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.playStoreConfig //Add your own signing config
        }

        debug {
            clean
            debuggable true
            manifestPlaceholders = [crashlyticsCollectionEnabled: "false"]
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding = true
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    lintOptions {
        checkReleaseBuilds false
        disable 'MissingTranslation'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {

    //add zebra jar file to dependencies
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //zebra dependency
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.0'

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.exifinterface:exifinterface:1.3.3'
    implementation "androidx.legacy:legacy-preference-v14:1.0.0"
    implementation "androidx.preference:preference:1.1.1"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.core:core-ktx:1.6.0"
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'com.google.firebase:firebase-analytics:19.0.2'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.3'

    implementation 'com.google.zxing:core:3.3.3'

    implementation 'com.github.phenoapps:phenolib:v0.9.29'
    implementation 'com.github.breeding-insight:brapi:2.0.3'

    implementation 'org.jsoup:jsoup:1.8.1'
    implementation 'net.sourceforge.jexcelapi:jxl:2.6.10'
    implementation 'pub.devrel:easypermissions:3.0.0'
    implementation('com.journeyapps:zxing-android-embedded:3.6.0') { transitive = false }
    implementation 'com.h6ah4i.android.preference:numberpickerprefcompat:1.0.0'
    //noinspection GradleDependency
    implementation 'com.github.MFlisar:changelog:1.1.6'
    implementation 'com.github.daniel-stoneuk:material-about-library:2.4.2'
    //noinspection GradleDependency
    implementation "com.mikepenz:aboutlibraries:6.2.3"
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.13.2'
    implementation 'com.github.ByteHamster:SearchPreference:v2.0.0'
    implementation "com.github.skydoves:colorpickerpreference:2.0.4"
    implementation 'com.github.evrencoskun:TableView:v0.8.9.4'

    implementation 'net.openid:appauth:0.9.0'
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava' //google messed up some packages, this package is temporary until the issue is fixed (Feb 2021)

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0"

    implementation 'math.geom2d:javaGeom:0.11.1'

    kapt 'com.android.databinding:compiler:3.1.4'

    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'junit:junit:4.13.2'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
}

repositories {
    mavenCentral()
}

task prepareKotlinBuildScriptModel {}

android.defaultConfig.manifestPlaceholders = ['appAuthRedirectScheme': 'fieldbook' ]
