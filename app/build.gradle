apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'com.google.dagger.hilt.android'

Properties props = new Properties()
props.load(new FileInputStream("$project.rootDir/version.properties"))
props.each { prop ->
    project.ext.set(prop.key, prop.value)
}

private Integer makeVersionCode() {
    int major = ext.majorVersion as Integer
    int minor = ext.minorVersion as Integer
    int patch = ext.patchVersion as Integer

    return major * 10000 + minor * 100 + patch
}

private String makeVersionName() {
    return "${ext.majorVersion}.${ext.minorVersion}.${ext.patchVersion}"
}

android {
    signingConfigs {
        playStoreConfig {
            Properties keystoreProps = new Properties()
            if (file('keystore.config').exists()) {
                keystoreProps.load(new FileInputStream(file('keystore.config')))

                keyAlias keystoreProps['keyAlias']
                keyPassword keystoreProps['keyPassword']
                storePassword keystoreProps['storePassword']
                storeFile file('keystore.keystore')
            } else println("Non-keystore build.")
        }
        gitActionConfig {
            def tmpFilePath = System.getProperty("user.home") + "/work/_temp/keystore/"
            def allFilesFromDir = new File(tmpFilePath).listFiles()

            if (allFilesFromDir != null) {
                def keystoreFile = allFilesFromDir.first()
                keystoreFile.renameTo("keystore/your_keystore.jks")
            }

            storeFile = file("keystore/your_keystore.jks")
            storePassword System.getenv("SIGNING_STORE_PASSWORD")
            keyAlias System.getenv("SIGNING_KEY_ALIAS")
            keyPassword System.getenv("SIGNING_KEY_PASSWORD")
        }
    }

    compileSdkVersion 33

    defaultConfig {
        applicationId "com.fieldbook.tracker"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode = makeVersionCode()
        versionName = makeVersionName()
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        release {
            minifyEnabled false
            debuggable false
            //signingConfig signingConfigs.playStoreConfig //Add your own signing config
        }

        debug {
            clean
            debuggable true
            manifestPlaceholders = [crashlyticsCollectionEnabled: "false"]
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding = true
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    lintOptions {
        checkReleaseBuilds false
        disable 'MissingTranslation'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            unitTests.returnDefaultValues = true
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {

    //add zebra jar file to dependencies
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')

    //zebra dependency
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.0'

    implementation 'androidx.appcompat:appcompat:1.7.0-alpha02'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.exifinterface:exifinterface:1.3.3'
    implementation "androidx.legacy:legacy-preference-v14:1.0.0"
    implementation "androidx.preference:preference:1.1.1"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.core:core-ktx:1.10.1"
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'com.google.firebase:firebase-analytics:19.0.2'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.3'

    implementation 'com.google.zxing:core:3.3.3'

    implementation('com.github.phenoapps:phenolib:v0.9.49')

    implementation 'com.google.android.exoplayer:exoplayer:2.17.0'
    implementation 'com.arthenica:ffmpeg-kit-min:5.1.LTS'
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'

    //uvc camera java native libs
    implementation("com.serenegiant:common:2.12.4")

    //brapi and required dependencies
    implementation 'com.github.breeding-insight:brapi:2.0.3'
    //necessary when building phenolib locally
    implementation "com.squareup.okhttp:okhttp:2.7.5"
    implementation("com.squareup.okhttp3:logging-interceptor:4.11.0")
    implementation("io.gsonfire:gson-fire:1.8.4") //required v1

    implementation 'org.jsoup:jsoup:1.8.1'
    implementation 'net.sourceforge.jexcelapi:jxl:2.6.10'
    implementation 'pub.devrel:easypermissions:3.0.0'
    implementation('com.journeyapps:zxing-android-embedded:3.6.0') { transitive = false }
    implementation 'com.h6ah4i.android.preference:numberpickerprefcompat:1.0.0'
    //noinspection GradleDependency
    implementation 'com.github.MFlisar:changelog:1.1.6'
    implementation 'com.github.daniel-stoneuk:material-about-library:2.4.2'
    //noinspection GradleDependency
    implementation "com.mikepenz:aboutlibraries:6.2.3"
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.13.2'
    implementation 'com.github.ByteHamster:SearchPreference:v2.0.0'
    implementation "com.github.skydoves:colorpickerpreference:2.0.4"
    implementation 'com.github.evrencoskun:TableView:v0.8.9.4'

    implementation 'net.openid:appauth:0.11.1'
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava' //google messed up some packages, this package is temporary until the issue is fixed (Feb 2021)

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0"

    implementation "androidx.fragment:fragment-ktx:1.4.1"
    implementation 'math.geom2d:javaGeom:0.11.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.2'
    implementation "androidx.navigation:navigation-dynamic-features-fragment:2.4.2"
    implementation "androidx.navigation:navigation-runtime-ktx:2.4.2"

    implementation 'androidx.documentfile:documentfile:1.0.1'

    //hilt dependency injection
    implementation "com.google.dagger:hilt-android:2.44"
    implementation 'androidx.test.ext:junit-ktx:1.1.4'

    kapt "com.google.dagger:hilt-compiler:2.44"
    kapt 'com.android.databinding:compiler:3.1.4'

    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'

    // Core library
    androidTestImplementation "androidx.test:core:$coreVersion"

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation "androidx.test:runner:$runnerVersion"
    androidTestImplementation "androidx.test:rules:$testRulesVersion"

    // Assertions
    androidTestImplementation "androidx.test.ext:junit:$extJUnitVersion"
    androidTestImplementation "androidx.test.ext:truth:$extTruthVersion"

    // Espresso dependencies
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-accessibility:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-web:$espressoVersion"
    androidTestImplementation "androidx.test.espresso.idling:idling-concurrent:$espressoVersion"

    // The following Espresso dependency can be either "implementation",
    // or "androidTestImplementation", depending on whether you want the
    // dependency to appear on your APKâ€™"s compile classpath or the test APK
    // classpath.
    androidTestImplementation "androidx.test.espresso:espresso-idling-resource:$espressoVersion"

    // For instrumented tests.
    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.44'
    // ...with Kotlin.
    kaptAndroidTest 'com.google.dagger:hilt-android-compiler:2.44'


    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
}

kapt {
    //used in hilt documentation says 'Allow references to generated code"
    correctErrorTypes true
}

repositories {
    mavenCentral()
}

task prepareKotlinBuildScriptModel {}

android.defaultConfig.manifestPlaceholders = ['appAuthRedirectScheme': 'fieldbook' ]
